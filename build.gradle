/*
 * Copyright (C) 2013-2014 Olaf Lessenich
 * Copyright (C) 2014-2015 University of Passau, Germany
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 2.1 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston,
 * MA 02110-1301  USA
 *
 * Contributors:
 *     Olaf Lessenich <lessenic@fim.uni-passau.de>
 *     Georg Seibt <seibt@fim.uni-passau.de>
 */

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'res'
        }
    }
    test {
        java {
            srcDir 'test'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'commons-io:commons-io:2.4'
    compile 'commons-cli:commons-cli:1.2'
    compile 'com.thoughtworks.xstream:xstream:1.4.8'
    compile 'de.uni-passau.fim.seibt:kvconfig:1.0'
    compile files('lib/JJ7.jar')
    testCompile 'junit:junit:4.12'
}

def configFile = file('JDime.properties')
def loggingConfigFile = file('JDimeLogging.properties')

task config {
    if (configFile.exists()) {
        inputs.files(configFile)
        outputs.files(configFile)
    }

    if (loggingConfigFile.exists()) {
        inputs.files(loggingConfigFile)
        outputs.files(loggingConfigFile)
    }
}

startScripts {
    applicationName = rootProject.name
    mainClassName = "de.fosd.jdime.Main"
    defaultJvmOpts = ["-Xmx2048m", "-ea", "-Djava.library.path=.", "-Djava.util.logging.config.file=${loggingConfigFile.getName()}"]
}

task guiScripts(type: CreateStartScripts) {
    applicationName = "${rootProject.name}_GUI"
    mainClassName = 'de.fosd.jdime.gui.GUI'
    defaultJvmOpts = ["-Djava.util.logging.config.file=${loggingConfigFile.getName()}"]
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath
}

distributions {
    main {
        contents.with {
            def binDir = 'bin'

            into(binDir) {
                from(guiScripts)
                fileMode = 0755
            }
            into(binDir) {
                from(config)
            }
        }
    }
    test {
        contents.with {
            def binDir = 'bin'

            into(binDir) {
                from(guiScripts)
                fileMode = 0755
            }
            into(binDir) {
                from(config)
            }
        }
    }
}

run {
    main = 'de.fosd.jdime.Main'

    if (System.getProperty("exec.args") != null) {
        args System.getProperty("exec.args").split()
    }
}

test {
    jvmArgs = ["-Xmx2048m", "-ea", "-Djava.library.path=build/install/JDime/bin"]
}

test.dependsOn installDist

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            // options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
}
