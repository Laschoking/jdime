apply plugin: 'java'
apply plugin: 'application'

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
}

repositories {
    mavenCentral()
    maven { // for glpk-java
        name = 'Xypron Release'
        url = 'http://rsync.xypron.de/repository'
    }
}

dependencies {
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'commons-cli:commons-cli:1.2'
    compile 'org.gnu.glpk:glpk-java:1.0.37'
    compile 'log4j:log4j:1.2.17'
    compile files('lib/JJ7.jar', 'lib/btc-ascii-table-1.0.jar')
}

def glpk_version = "4_55"
def glpk_java_prop_name = 'glpkJava'
def glpk_prop_name = 'glpk'

jar.doLast {
    def fail = false;
    
    if (!rootProject.hasProperty(glpk_java_prop_name)) {
        println "Provide the path to the glpk_${glpk_version}_java native library using the ${glpk_java_prop_name} property."
        fail = true;
    }     
    if (!rootProject.hasProperty(glpk_prop_name)) {
        println "Provide the path to the glpk_${glpk_version} native library using the ${glpk_prop_name} property."
        fail = true;
    }
    
    if (fail) {
        throw new InvalidUserDataException("Missing native library paths.");
    }
}

task natives {
    if (rootProject.hasProperty(glpk_java_prop_name) && rootProject.hasProperty(glpk_prop_name)) {
        def glpkNat = file(glpk)
        def glpkJavaNat = file(glpkJava)

        inputs.files(glpkNat, glpkJavaNat)
        outputs.files(glpkNat, glpkJavaNat)
    }
}

applicationName = rootProject.name
mainClassName = "de.fosd.jdime.Main"

applicationDistribution.from(natives) {
    into("bin")
}
